---
title: Integer Volutes in BQN
date: 2024-11-08
tags: BQN
---

On [day 3](https://tony-zorman.com/posts/aoc-in-bqn.html#day-3)
of my post about doing some
[Advent of Code problems in BQN](https://tony-zorman.com/posts/aoc-in-bqn.html),
the task was to walk along some *integer volutes*:
spirals, either spiralling inwardsâ€”involutesâ€”or outwardsâ€”evolutesâ€”in either direction.
While the specific problem didn't require it,
I think it's a pretty neat exercise to think about how to generate these things.
Let's talk about that.

<!--more-->

For example, the following is a left-spiraling evolute.

``` bqn
    Evolute 9
â”Œâ”€
â•µ 64 63 62 61 60 59 58 57 56
  65 36 35 34 33 32 31 30 55
  66 37 16 15 14 13 12 29 54
  67 38 17  4  3  2 11 28 53
  68 39 18  5  0  1 10 27 52
  69 40 19  6  7  8  9 26 51
  70 41 20 21 22 23 24 25 50
  71 42 43 44 45 46 47 48 49
  72 73 74 75 76 77 78 79 80
                             â”˜
```

Quite pretty, isn't it?

Before we start, a big disclaimer:
this is neither my work, nor did I come up with any of the novel insights presented here.
Rather, there is an article by Eugene McDonnell (available
[here](https://code.jsoftware.com/wiki/Doc/Articles/Play132)
or [here](https://www.jsoftware.com/papers/play132.htm))
who talks about a solution to this problem by Joey Tuttle.[^8]
Both of the links above are not entirely satisfactory digitisations of the original article,
so in writing this post I'm trying to contribute a perhaps more palatable version.[^11]

This post is written in the REPL-like fashion that I've already used above:
input is indented by four spaces,
and output is flush to the left.
As such, there will be lots of examples that highlight certain aspects of the current sub-problem to be solved.

Finally, the article probably won't make much sense unless you know a little bit of BQN already,
the basics of which are probably best learned from the
[official website](https://mlochbaum.github.io/BQN/).

# The coordinate based solution

Let's initially produce a coordinate-based solution.
Our first sub-goal is to generate the correct indices in order.
Put the origin `âŸ¨0 0âŸ©` into the middle, at the number `0`.
Our coordinate system will be that of indexing into the array; i.e.,
`0â€¿1`, `Â¯1â€¿0`, `0â€¿Â¯1`, and `1â€¿0` represent east, north, west, and south, respectively.
Looking at the 9Ã—9 spiral above,
let's think about what kind of steps we're taking:

```
0  -E->  1  -N->  2  -W->  3  -W->  4  -S->  5  -S->  6  -E->  7  -E->  8 â€¦
```

Just writing out the directions yields something like `ENWWSSEEENNNWWWWSSSSâ€¦`.
We alternate between east, north, west, and south in that order,
and always bunch up two of them with the same frequencyâ€”first one, then two, etc.
In particular, we "break into" a new iteration in the spiralling
every time a number is increased:

```
0     0 1       2     4 3 2     4 3 2     4 3 2
              0 1       0 1     5 0 1     5 0 1
                                6         6 7 8 9
```

If we want to end up with a square,
our sequence would look like \(1, 1, 2, 2, â€¦ k-1, k-1, k, k, k\);
repeating the last number three times instead of continuing with \(k+1\).

All in all, we need to generate two sequences of numbers:
the coordinates, and their associated frequencies.
Given a grid of size \(n\), some quick maths tells us that
we have to generate a sequence with \(2n - 1\) directions.[^7]

``` bqn
    { (1-Ëœğ•©Ã—2)â¥ŠâŸ¨0â€¿1,Â¯1â€¿0,0â€¿Â¯1,1â€¿0âŸ© } 3 # Generate this many indices
âŸ¨ âŸ¨ 0 1 âŸ© âŸ¨ Â¯1 0 âŸ© âŸ¨ 0 Â¯1 âŸ© âŸ¨ 1 0 âŸ© âŸ¨ 0 1 âŸ© âŸ©
    {(ğ•©-1)âˆ¾Ëœ2/1+â†•ğ•©-1} 3 # Double with an extra bit at the end
âŸ¨ 1 1 2 2 2 âŸ©
```

For doubling a given vector of numbers,
we can conveniently use
*replicate* ([`/`](https://mlochbaum.github.io/BQN/doc/replicate.html)),
and then manually add the number that should be repeated three times to the end.

``` bqn
    { ğ•Šn: # Smush everything together.
      ((n-1)âˆ¾Ëœ2/1+â†•n-1){<Ë˜ğ•¨â€¿2â¥Šğ•©}Â¨(1-ËœnÃ—2)â¥ŠâŸ¨0â€¿1,Â¯1â€¿0,0â€¿Â¯1,1â€¿0âŸ©
    } 3
â”Œâ”€
Â· âŸ¨ âŸ¨ 0 1 âŸ© âŸ© âŸ¨ âŸ¨ Â¯1 0 âŸ© âŸ© âŸ¨ âŸ¨ 0 Â¯1 âŸ© âŸ¨ 0 Â¯1 âŸ© âŸ© âŸ¨ âŸ¨ 1 0 âŸ© âŸ¨ 1 0 âŸ© âŸ© âŸ¨ âŸ¨ 0 1 âŸ© âŸ¨ 0 1 âŸ© âŸ©
                                                                                         â”˜
    { ğ•Šn: # Flatten.
      âˆ¾((n-1)âˆ¾Ëœ2/1+â†•n-1){<Ë˜ğ•¨â€¿2â¥Šğ•©}Â¨(1-ËœnÃ—2)â¥ŠâŸ¨0â€¿1,Â¯1â€¿0,0â€¿Â¯1,1â€¿0âŸ©
    } 3
âŸ¨ âŸ¨ 0 1 âŸ© âŸ¨ Â¯1 0 âŸ© âŸ¨ 0 Â¯1 âŸ© âŸ¨ 0 Â¯1 âŸ© âŸ¨ 1 0 âŸ© âŸ¨ 1 0 âŸ© âŸ¨ 0 1 âŸ© âŸ¨ 0 âŸ© âŸ©
```

Smushing everything together works by
*reshaping* ([`â¥Š`](https://mlochbaum.github.io/BQN/doc/reshape.html))
the vector of length two into an array with two columns,
and as many rows as the desired frequency.
Flattening everything is done by calling
*enclose* ([`<`](https://mlochbaum.github.io/BQN/doc/enclose.html))
on each major cell.

``` bqn
    3 {ğ•¨â€¿2â¥Šğ•©} Â¯1â€¿0
â”Œâ”€
â•µ Â¯1 0
  Â¯1 0
  Â¯1 0
       â”˜
    3 {<Ë˜ğ•¨â€¿2â¥Šğ•©} Â¯1â€¿0
âŸ¨ âŸ¨ Â¯1 0 âŸ© âŸ¨ Â¯1 0 âŸ© âŸ¨ Â¯1 0 âŸ© âŸ©
```

This yields the function to generate a vector of directions.

``` bqn
    Dirs â† { ğ•Šn: # Directions for an nÃ—n grid
      âˆ¾((ğ•©-1)âˆ¾Ëœ2/1+â†•ğ•©-1){<Ë˜ğ•¨â€¿2â¥Šğ•©}Â¨(1-ËœnÃ—2)â¥ŠâŸ¨0â€¿1,Â¯1â€¿0,0â€¿Â¯1,1â€¿0âŸ©
    }
(function block)
```

To get the coordinates, we just have to "walk the path"â€”that is,
compute the
`+`-*scan* ([`` ` ``](https://mlochbaum.github.io/BQN/doc/scan.html))
and add the origin.

``` bqn
    Coords â† { (<0â€¿0)âˆ¾ +` Dirsğ•© }
(function block)
    Coords 3
âŸ¨ âŸ¨ 0 0 âŸ© âŸ¨ 0 1 âŸ© âŸ¨ Â¯1 1 âŸ© âŸ¨ Â¯1 0 âŸ© âŸ¨ Â¯1 Â¯1 âŸ© âŸ¨ 0 Â¯1 âŸ© âŸ¨ 1 Â¯1 âŸ© âŸ¨ 1 0 âŸ© âŸ¨ 1 1 âŸ© âŸ©
```

These coordinates are associated to the numbers that their respective index represents:
`0` for `âŸ¨0 0âŸ©`, `1` for `âŸ¨0 1âŸ©`, and so on.

``` bqn
    (â†•9) â‰ Coords 3
â”Œâ”€
â•µ 0       1       2        3        4         5        6        7       8
  âŸ¨ 0 0 âŸ© âŸ¨ 0 1 âŸ© âŸ¨ Â¯1 1 âŸ© âŸ¨ Â¯1 0 âŸ© âŸ¨ Â¯1 Â¯1 âŸ© âŸ¨ 0 Â¯1 âŸ© âŸ¨ 1 Â¯1 âŸ© âŸ¨ 1 0 âŸ© âŸ¨ 1 1 âŸ©
                                                                                â”˜
```

All that's left to do is to *reshape* the argument into the correct form.
This uses the fact that coordinates have a nice lexicographic ordering.

``` bqn
    â†•3â€¿3          # Origin top left
â”Œâ”€
â•µ âŸ¨ 0 0 âŸ© âŸ¨ 0 1 âŸ© âŸ¨ 0 2 âŸ©
  âŸ¨ 1 0 âŸ© âŸ¨ 1 1 âŸ© âŸ¨ 1 2 âŸ©
  âŸ¨ 2 0 âŸ© âŸ¨ 2 1 âŸ© âŸ¨ 2 2 âŸ©
                          â”˜
    (â†•3â€¿3) - <1â€¿1 # Origin in the middle, where we want it
â”Œâ”€
â•µ âŸ¨ Â¯1 Â¯1 âŸ© âŸ¨ Â¯1 0 âŸ© âŸ¨ Â¯1 1 âŸ©
  âŸ¨ 0 Â¯1 âŸ©  âŸ¨ 0 0 âŸ©  âŸ¨ 0 1 âŸ©
  âŸ¨ 1 Â¯1 âŸ©  âŸ¨ 1 0 âŸ©  âŸ¨ 1 1 âŸ©
                              â”˜
```

We first order the coordinates by their columns (the first number), and then by their rows (the second).
A verbose way of doing that would be the following:[^9]

``` bqn
    (Coords 3) â‹ˆÂ¨ â†•9        # Zip coordinates to indices
â”Œâ”€
Â· âŸ¨ âŸ¨ 0 0 âŸ© 0 âŸ© âŸ¨ âŸ¨ 0 1 âŸ© 1 âŸ© âŸ¨ âŸ¨ Â¯1 1 âŸ© 2 âŸ© âŸ¨ âŸ¨ Â¯1 0 âŸ© 3 âŸ© âŸ¨ âŸ¨ Â¯1 Â¯1 âŸ© 4 âŸ©
  âŸ¨ âŸ¨ 0 Â¯1 âŸ© 5 âŸ© âŸ¨ âŸ¨ 1 Â¯1 âŸ© 6 âŸ© âŸ¨ âŸ¨ 1 0 âŸ© 7 âŸ© âŸ¨ âŸ¨ 1 1 âŸ© 8 âŸ©
                                                                            â”˜
    âˆ§ (Coords 3) â‹ˆÂ¨ â†•9      # Sort
â”Œâ”€
Â· âŸ¨ âŸ¨ Â¯1 Â¯1 âŸ© 4 âŸ© âŸ¨ âŸ¨ Â¯1 0 âŸ© 3 âŸ© âŸ¨ âŸ¨ Â¯1 1 âŸ© 2 âŸ© âŸ¨ âŸ¨ 0 Â¯1 âŸ© 5 âŸ© âŸ¨ âŸ¨ 0 0 âŸ© 0 âŸ©
  âŸ¨ âŸ¨ 0 1 âŸ© 1 âŸ© âŸ¨ âŸ¨ 1 Â¯1 âŸ© 6 âŸ© âŸ¨ âŸ¨ 1 0 âŸ© 7 âŸ© âŸ¨ âŸ¨ 1 1 âŸ© 8 âŸ©
                                                                            â”˜
    1âŠ‘Â¨ âˆ§ (Coords 3) â‹ˆÂ¨ â†•9  # Get indices
âŸ¨ 4 3 2 5 0 1 6 7 8 âŸ©
    3â€¿3â¥Š1âŠ‘Â¨âˆ§(Coords 3)â‹ˆÂ¨â†•9  # Reshape
â”Œâ”€
â•µ 4 3 2
  5 0 1
  6 7 8
        â”˜
```

This works, but feels a bit unsatisfactory.
We seem to be doing a lot of extra work just to get the correct ordering of the indices.
Luckily, BQN has a primitive functionâ€”two, actuallyâ€”that exactly encapsulates operations like the above:
a *grade*.
Briefly,
given an array with major cells \((c_1, \dots, c_n)\),
*grade up* ([`â‹`](https://mlochbaum.github.io/BQN/doc/order.html#grade))
returns a permutation \((i_1, \dots, i_n)\) of the indices,
which yields the order that would sort the cells:
\(c_{i_1}\) is the smallest element, \(c_{i_2}\) is the second smallest, and so on.
This can be used to sort the vector by *picking* each \(i_j\) in order.[^3]

``` bqn
    a â† âŸ¨3,1,0,4,4,6,7,19,16âŸ©
âŸ¨ 3 1 0 4 4 6 7 19 16 âŸ©
    g â† â‹a
âŸ¨ 2 1 0 3 4 5 6 8 7 âŸ©
    (âŠ‘g)âŠ‘a    # smallest element
0
    (Â¯1âŠ‘g)âŠ‘a  # Largest element
19
    gâŠa       # Sort: g is a vector of indices into a
âŸ¨ 0 1 3 4 4 6 7 16 19 âŸ©
    # Grading is the same as sorting and getting the indices
    (â‹a) â‰¡ 1âŠ‘Â¨âˆ§aâ‹ˆÂ¨â†•9
1
```

Putting everything together, we can write the first version of `Evolute`.

``` bqn
    Evolute â† { ğ•©â€¿ğ•©â¥Š â‹ Coordsğ•© }
(function block)
    Evolute 3
â”Œâ”€
â•µ 4 3 2
  5 0 1
  6 7 8
        â”˜
    Evolute 5
â”Œâ”€
â•µ 16 15 14 13 12
  17  4  3  2 11
  18  5  0  1 10
  19  6  7  8  9
  20 21 22 23 24
                 â”˜
```

Nice.

# A flat solution

While I think `Evolute` is quite neat,
a fun aspect of array programming is to come up with
[completely](https://saltysylvi.github.io/blog/flat1.html)
[flat](https://saltysylvi.github.io/blog/flat2.html)
solutions to problems.
That is, solutions that do not use any nested arraysâ€”like vectors of coordinatesâ€”whatsoever.
This is often much more efficient,
and may yield new insights in how the problem actually works.
Plus, working in a more constrained setting can be fun just in and of itself.

Let's start with the flat version of the completed spiral.

``` bqn
    spiral â† â¥ŠEvolute 3
âŸ¨ 4 3 2 5 0 1 6 7 8 âŸ©
```

Naively, the general strategy is this:
throw transformations `fáµ¢` at `spiral`
until we arrive at some vector `v` that is reasonably easy to generate;
i.e., `v â‰¡ fâ‚ â€¦ fâ‚™ spiral`.
Then, invert all of the `fáµ¢` to re-generate the original vector:
`spiral â‰¡ fâ‚™â»Â¹ â€¦ fâ‚â»Â¹ v`.[^4]
As far as I can tell, one just needs enough intuition about these flat arrays to guess nice enough functions.

Now, from the first solution we already know that `spiral` is the *grade* of some list of coordinates,
which we however have no hope of recovering.
After enough staring, one notices that `spiral` itself looks like a permutation<!--
-->â€”why not try to find the inverse of that?
Thankfully this is very easy, as *grade up* is self-inverse in this case:[^5]

``` bqn
    â‹â‹spiral
âŸ¨ 4 3 2 5 0 1 6 7 8 âŸ©
    spiralâ‰¡â‹â‹spiral
1
    â‹spiral # This is the permutation we want.
âŸ¨ 4 5 2 1 0 3 6 7 8 âŸ©
```

I think that `â‹spiral` doesn't look any less random than `spiral`,
but Tuttle evidently saw something,
and instead checked whether this list was the running sum of a good one;
i.e., whether there exists some nice `y` such that ``(â‹spiral) â‰¡ +`y``.
This in particular means that we have to compute the inverse of ``+` ``:
subtract each element from its predecessor, or `-âŸœÂ»` for short.

``` bqn
    -âŸœÂ» â‹spiral
âŸ¨ 4 1 Â¯3 Â¯1 Â¯1 3 3 1 1 âŸ©
    +` -âŸœÂ» â‹spiral
âŸ¨ 4 5 2 1 0 3 6 7 8 âŸ©
    (â‹spiral) â‰¡ (+`-âŸœÂ»â‹spiral)
1
```

In fact, we can even ask BQN to do this for us by means of
*undo* ([`â¼`](https://mlochbaum.github.io/BQN/doc/undo.html)).

``` bqn
    +`â¼ â‹spiral
âŸ¨ 4 1 Â¯3 Â¯1 Â¯1 3 3 1 1 âŸ©
```

Finally, this looks like something we can generate!
Let's take a look at a 5Ã—5 grid, so the structure becomes even more obvious:

``` bqn
    +`â¼â‹â¥ŠEvolute 5
âŸ¨ 12 1 Â¯5 Â¯1 Â¯1 5 5 1 1 1 Â¯5 Â¯5 Â¯5 Â¯1 Â¯1 Â¯1 Â¯1 5 5 5 5 1 1 1 1 âŸ©
```

Ignoring the number at the start, we have an alternation of `1 Â¯n Â¯1 n`, where `n` is the size of the evolute.
Disregarding multiple occurrences and the first number,
there are exactly `(2Ã—n)-1` "primitive" numbers
to which we need to associate frequencies,
which matches with the coordinate generation from before.

``` bqn
    { (1-Ëœ2Ã—ğ•©)â¥ŠâŸ¨1,-ğ•©,Â¯1,ğ•©âŸ© } 3
âŸ¨ 1 Â¯3 Â¯1 3 1 âŸ©
    { (1-Ëœ2Ã—ğ•©)â¥ŠâŸ¨1,-ğ•©,Â¯1,ğ•©âŸ© } 5
âŸ¨ 1 Â¯5 Â¯1 5 1 Â¯5 Â¯1 5 1 âŸ©
```

These numbers grow in their frequency in blocks of two; first one of each, then two, and so on.
Everything here is entirely analogous to the coordinate case,
including the fact that we need to repeat the last number three times in order to obtain a square grid.

``` bqn
    { 2/1+â†•ğ•©-1 } 5
âŸ¨ 1 1 2 2 3 3 4 4 âŸ©
    { (ğ•©-1)âˆ¾Ëœ2/1+â†•ğ•©-1 } 5 # Need correcting term at the end
âŸ¨ 1 1 2 2 3 3 4 4 4 âŸ©
    { ((ğ•©-1)âˆ¾Ëœ2/1+â†•ğ•©-1)/(1-Ëœ2Ã—ğ•©)â¥ŠâŸ¨1,-ğ•©,Â¯1,ğ•©âŸ© } 3
âŸ¨ 1 Â¯3 Â¯1 Â¯1 3 3 1 1 âŸ©
```

As for the first number that we ignored so far: it's not actually important!
What we do with this list is a `+`-*scan*, followed by a *grade up* to sort the indices.
This means that only the relative value of the *scan* is important for the following *grade*,
so outside of special cases like `âˆ`, the first number could be almost anything.

``` bqn
    { 0âˆ¾((ğ•©-1)âˆ¾Ëœ2/1+â†•ğ•©-1)/(1-Ëœ2Ã—ğ•©)â¥ŠâŸ¨1,-ğ•©,Â¯1,ğ•©âŸ© } 5
âŸ¨ 0 1 Â¯5 Â¯1 Â¯1 5 5 1 1 1 Â¯5 Â¯5 Â¯5 Â¯1 Â¯1 Â¯1 Â¯1 5 5 5 5 1 1 1 1 âŸ©
```

In particular, this opens up a nice shortcut.
Instead of manually adding `ğ•©-1` to the back and `0` to the front,
we can just add `ğ•©` itself to the back,
and then *nudge* the whole array to the right,
inserting the filler `0` as the first element.

``` bqn
    { 0âˆ¾((ğ•©-1)âˆ¾Ëœ2/1+â†•ğ•©-1) } 5
âŸ¨ 0 1 1 2 2 3 3 4 4 4 âŸ©
    { Â»(ğ•©âˆ¾Ëœ2/1+â†•ğ•©-1) } 5
âŸ¨ 0 1 1 2 2 3 3 4 4 âŸ©
    { Â»(Â¯1â†“2/1+â†•ğ•©) } 5
âŸ¨ 0 1 1 2 2 3 3 4 4 âŸ©
    { Â»(Â¯1â†“2/1+â†•ğ•©)/(1-Ëœ2Ã—ğ•©)â¥ŠâŸ¨1,-ğ•©,Â¯1,ğ•©âŸ© } 5
âŸ¨ 0 1 Â¯5 Â¯1 Â¯1 5 5 1 1 1 Â¯5 Â¯5 Â¯5 Â¯1 Â¯1 Â¯1 Â¯1 5 5 5 5 1 1 1 1 âŸ©
```

We obtain a completely flat version of `Evolute`,
which works just like the original function.

``` bqn
    EvoluteFlat â† { ğ•©â€¿ğ•©â¥Šâ‹+`Â»(Â¯1â†“2/1+â†•ğ•©)/(1-Ëœ2Ã—ğ•©)â¥ŠâŸ¨1,-ğ•©,Â¯1,ğ•©âŸ© }
(function block)
    EvoluteFlat 3
â”Œâ”€
â•µ 4 3 2
  5 0 1
  6 7 8
        â”˜
    EvoluteFlat 5
â”Œâ”€
â•µ 16 15 14 13 12
  17  4  3  2 11
  18  5  0  1 10
  19  6  7  8  9
  20 21 22 23 24
                 â”˜
```

Getting a rightwards spiral just involves
twiddling with the vector `âŸ¨1,-ğ•©,Â¯1,ğ•©âŸ©` a little bit.
I'll leave that as an exercise for the interested reader.

# Conclusion

Here's a complete definition of both solutions.

``` bqn
E1 â† {ğ•©â€¿ğ•©â¥Šâ‹(<0â€¿0)âˆ¾+`âˆ¾((2/1+â†•ğ•©-2)âˆ¾3â¥Šğ•©-1){<Ë˜ğ•¨â€¿2â¥Šğ•©}Â¨(1-Ëœğ•©Ã—2)â¥ŠâŸ¨0â€¿1,Â¯1â€¿0,0â€¿Â¯1,1â€¿0âŸ©}
E2 â† {ğ•©â€¿ğ•©â¥Šâ‹+`Â»(Â¯1â†“2/1+â†•ğ•©)/(1-Ëœ2Ã—ğ•©)â¥ŠâŸ¨1,-ğ•©,Â¯1,ğ•©âŸ©}
```

I certainly think that `E2` is much prettier than `E1`;
not only because it's shorter, but also because there seems to be less wrangling to get everything to line up correctly.
Seeing these side by side does reveal that they use a lot of the same techniques,
however, which I think is interesting.

---

As a bonus, here is the mandatory golfed solution,
this time as an involute.

``` bqn
    G â† {(âŠ’Ëœâˆ¾â‰âˆ˜âŒ½+â‰ )âŸ2âŸğ•©â†•0â€¿0}
(function block)
    SLG â† {(â†•âˆ˜â‰ âˆ¾â‰âˆ˜âŒ½+â‰ )âŸ(2Ã—ğ•©)â†•0â€¿0} # Slighly less golfed
(function block)
    G 5
â”Œâ”€
â•µ  0  1  2  3 4
  15 16 17 18 5
  14 23 24 19 6
  13 22 21 20 7
  12 11 10  9 8
                â”˜
    (SLG 5) â‰¡ G 5
1
```

I will leave it to the interested reader to figure out that oneâ€”or to consult the
[relevant StackExchange thread](https://codegolf.stackexchange.com/questions/241803/print-a-nxn-integer-involute),
which I perhaps should have looked up before writing this whole thing.
Still, all of this transposing and reversing the matrix means that this solution is on the slower side.
Here's a quick an dirty comparison:

``` bqn
    )time G   1000  # Golfed version
1.0114s
    )time G   2000
13.885s
    )time SLG 1000  # Slightly less golfed version
651.3ms
    )time SLG 2000
8.1603s
    )time E1  1000  # Using indices
218ms
    )time E1  2000
950.2ms
    )time E2  1000  # Flat
4.197ms
    )time E2  2000
12.98ms
```

While both are cubic and thus degrade fast,
`SLG` is faster than `G` because `â†•â‰ ` to generate a list of integers is much quicker than some *progressive index of* shenanigans to do the same thing.
At the end of the day, I think I prefer the flat solution :)

[^3]: In fact, in APL this is the only way to do thisâ€”there is no other sorting primitive!

[^4]: Note that, in particular, we are interested in left inverses of all the `fáµ¢`.
      This is a bit in opposition to the facilities that BQN provides by default, like
      *undo* to find the right inverse.
      However, we'll manage.

[^5]: A quick justification:
      suppose that we have a vector \(n \defeq (n_1, \dots, n_k)\) of natural numbers,
      such that every number from \(0\) to \(k-1\) appears exactly once.
      Using *grade up*, we obtain a permutation \(\sigma \defeq (i_1, \dots, i_k)\).
      If one *grades* that permutation in the same direction,
      then whatever index \(i_j\) has value \(0\)
      will be the very first element of the new permutation \(\tau \defeq (i_j, \dots, i_\ell)\),
      and likewise for the other numbers.
      Thus, \(\tau \sigma (n) = n\).

      Of course, `â‹â‹` it not the identity in general, but it is a useful operation nonetheless:
      it "ranks" the elements of the input,
      with `0` corresponding to the smallest element, and so on.
      This is known as the
      [ordinal idiom](https://mlochbaum.github.io/BQN/doc/order.html#ordinals).

[^7]: The frequencies have to add up to \(n^2 - 1\), covering all points but the origin.
      A given sequence \(1, 1, 2, 2, \dots, k-1, k-1, k, k, k\) has \(2k + 1\) members
      and sums up to \(k^2 + 2k\).
      Solving \(k^2 + 2k = n^2 - 1\) for \(k\) has sensible roots,
      \(\pm n - 1\), of which we of course pick \(n-1\).
      Substituting \(k=n-1\),
      we have to generate \(2n - 1\) frequencies,
      the values of which indeed sum up to \((n-1)^2 + 2(n-1) = n^2 - 1\).

[^8]: Special thanks to Marshall Lochbaum for providing these links.

[^9]: {-} Line breaks in the output for clarity.

[^11]: Plus, I don't know any J, so reading the article in detail is a lot more work than just writing it in BQN myself.
